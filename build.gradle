plugins {
    id "java"
    id "io.qameta.allure" version "${allurePluginVersion}"
    id 'com.github.jk1.dependency-license-report' version '1.16'
}

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}

tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}

dependencies {

    // TODO: remove lombok from runtime scope
    annotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    implementation("org.projectlombok:lombok:${lombokVersion}")

    // test scope
    testAnnotationProcessor("org.projectlombok:lombok:${lombokVersion}")
    testImplementation("org.projectlombok:lombok:${lombokVersion}")

    testImplementation(platform("org.junit:junit-bom:${junitVersion}"))
    testImplementation("org.junit.jupiter:junit-jupiter-api")
    testImplementation("org.junit.jupiter:junit-jupiter-engine")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testImplementation("org.junit.platform:junit-platform-launcher")

    testImplementation("org.assertj:assertj-core:${assertjVersion}")
    testImplementation("io.reactivex.rxjava3:rxjava:3.0.7")
    testImplementation("io.projectreactor:reactor-core:3.4.0")
}

repositories {
    jcenter()
}

allure {
    autoconfigure = true
    version = "${allureVersion}"  // Latest Allure Version

    useJUnit5 {
        version = "${allureVersion}" // Latest Allure Version
    }

}

test {

    useJUnitPlatform {
        excludeTags "integration"
        includeEngines "junit-jupiter"
    }

    testLogging {
        events "failed"
        //exceptionFormat "full"
        exceptionFormat "short"
    }

    enableAssertions = true
    testLogging.showStandardStreams = false

    include "**/**Test.*"
    include "**/**Tests.*"

    afterTest { desc, result ->
        //org.gradle.api.internal.tasks.testing.results.DefaultTestResult
        /*if (result.resultType == TestResult.ResultType.FAILURE) {
            result.failures.each { failure ->
                def sep = ("=" * 80)
                if (failure instanceof Throwable) {
                    StringWriter writer = new StringWriter()
                    failure.printStackTrace(new PrintWriter(writer))
                    failure = writer.toString()
                    writer.close()
                }
                System.err.printf("%n${sep}%n${failure}%n${sep}%n%n")
            }
        }*/
    }

    afterSuite { desc, result ->
        if (!desc.parent && result.testCount > 0) { // will match the outermost suite

            def output = "${project.name}: ${result.resultType} (${result.testCount} tests"
            if (result.successfulTestCount > 0 && result.testCount != result.successfulTestCount)
                output += ", ${result.successfulTestCount} successes"
            if (result.failedTestCount > 0)
                output += ", ${result.failedTestCount} failures"
            if (result.skippedTestCount > 0)
                output += ", ${result.skippedTestCount} skipped"
            output += ")"

            def startItem = "| "
            def endItem = " |"
            def sep = ("-" * (startItem.length() + output.length() + endItem.length()))

            (result.failedTestCount > 0 ? System.err : System.out).printf("%n${sep}%n${startItem}${output}${endItem}%n${sep}%n%n")
        }
    }
}

wrapper {
    doFirst {
        println "Gradle Version: ${gradlewVersion}"
    }
    gradleVersion = "${gradlewVersion}"
    distributionType = Wrapper.DistributionType.ALL
}
